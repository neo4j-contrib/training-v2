= Exercise 4
:icons: font

== Exercise 4: Creating the `Flight` node from the `CONNECTED_TO` relationships  (Preparations)

Before you begin this exercise, make sure that you have loaded the *flights_2019_1k* data in Exercise 2.

This is what you should see when you click the database icon image:{guides}/img/database-icon.png[].

image::{guides}/img/AfterExercise2.png[AfterExercise2,width=150,role=left]

{nbsp} +

*If your database does not have the same nodes and relationships, you can execute these Cypher statements to reset your graph to what it should be before you start this exercise.*

[source,cypher]
----
include::scripts/AfterExercise_2.cypher[]
----

*Note*: You must use the "file:///" syntax for loading the data from the import folder.

== Exercise 4: Creating the `Flight` node from the `CONNECTED_TO` relationships (Overview)

You have seen from profiling a few queries that the current model needs refactoring.
You will refactor the current graph to fit the new model and then you will profile the queries again.

Here are the tasks you will perform:

* *Exercise 4.1*: Add an index to the `Airport.code` property.
* *Exercise 4.2*: Add a unique constraint to the `Flight.flightId` property.
* *Exercise 4.3*: Add an index to the `Flight.number` property.
* *Exercise 4.4*: Refactor the graph to create `Flight` nodes from the `CONNECTED_TO` relationships.
* *Exercise 4.5*: Profile our first query that finds all flights that land in Las Vegas.
* *Exercise 4.6*: Profile our second query that finds all flights for airline 'WN' with flight number '1016'.
* *Exercise 4.7*: Delete the `CONNECTED_TO` relationship from the graph.

Go to the next page to start this exercise.

== Exercise 4.1:  Add an index to the `Airport.code` property. (Instructions)

*Write and execute the Cypher statement to add an index to the Airport nodes based upon the code property.*

== Exercise 4.1:  Add an index to the `Airport.code` property. (Solution)

*Write and execute the Cypher statement to add an index to the `Airport` nodes based upon the code property.*

[source,cypher]
----
CREATE INDEX Airport_code_index FOR (a:Airport) ON (a.code)
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex4_AirportCodeIndex.png[Ex4_AirportCodeIndex,width=300]


== Exercise 4.2:  Add a unique constraint to the `Flight.flightId` property. (Instructions)

You will be creating nodes of type `Flight`.
One of the properties you will adding to a `Flight` node is an ID that makes each `Flight` node unique.
The name of this property will be `flightId`.

*Write and execute the Cypher statement to add a unique constraint to the `Flight` nodes based upon the `flightId` property.*

== Exercise 4.2:  Add a unique constraint to the `Flight.flightId` property. (Solution)

You will be creating nodes of type `Flight`.
One of the properties you will adding to a `Flight` node is an ID that makes each `Flight` node unique.
The name of this property will be `flightId`.

*Write and execute the Cypher statement to add a unique constraint to the `Flight` nodes based upon the `flightId` property.*

[source,cypher]
----
CREATE CONSTRAINT Flight_flightId_constraint ON (f:Flight)
       ASSERT f.flightId IS UNIQUE
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex4_FlightIdConstraint.png[Ex4_FlightIdConstraint,width=300]


== Exercise 4.3:  Add an index to the `Flight.number` property. (Instructions)

*Write and execute the Cypher statement to add an index to the `Flight` nodes based upon the `number` property.*

== Exercise 4.3:  Add an index to the `Flight.number` property. (Solution)

*Write and execute the Cypher statement to add an index to the `Flight` nodes based upon the `number` property.*

[source,cypher]
----
CREATE INDEX Flight_number_index FOR (f:Flight) ON (f.number)
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex4_FlightNumberIndex.png[Ex4_FlightNumberIndex,width=300]


== Exercise 4.4:  Refactor the graph to create Flight nodes from the CONNECTED_TO relationships. (Instructions)

`Flight` nodes will be created from existing `CONNECTED_TO` relationships where you must keep track of the origin airport and the destination airport.
For example: `(origin:Airport)-[CONNECTED_TO]->(destination:Airport)`.
The properties for the `Flight` nodes will be set as follows:

* `flightId:` connection.airline +  connection.flightNumber +  `'_'` + connection.date +  `'_'` + origin.code + `'_'` + destination.code
* `date`: connection.date
* `airline`: connection.airline
* `number`: connection.flightNumber
* `departure`: connection.departure
* `arrival`: connection.arrival

In addition, the new relationships between `Flight` and `Airport` nodes will be `ORIGINATES_FROM` and `LANDS_IN`.

Here is the model  you will refactor to:

[.thumb]
image::{guides}/img/Ex4_Model.png[Ex4_Model,width=500]

{nbsp} +

*Write and execute the Cypher statement traverse all `CONNECTED_TO` relationships to create the `Flight` nodes.*

== Exercise 4.4:  Refactor the graph to create `Flight` nodes from the `CONNECTED_TO` relationships. (Solution)

*Write and execute the Cypher statement traverse all `CONNECTED_TO` relationships to create the `Flight` nodes.*

[source,cypher]
----
MATCH (origin:Airport)-[connection:CONNECTED_TO]->(destination:Airport)
MERGE (newFlight:Flight { flightId: connection.airline +  connection.flightNumber +
       '_' + connection.date +  '_' + origin.code + '_' + destination.code })
ON CREATE SET newFlight.date = connection.date,
              newFlight.airline = connection.airline,
              newFlight.number = connection.flightNumber,
              newFlight.departure = connection.departure,
              newFlight.arrival = connection.arrival
MERGE (origin)<-[:ORIGINATES_FROM]-(newFlight)
MERGE (newFlight)-[:LANDS_IN]->(destination)
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex4_Refactor.png[Ex4_Refactor,width=300]

== Exercise 4.5:  Profile our first query that finds all flights that land in Las Vegas. (Instructions)

The previous query was:

[source,cypher]
----
PROFILE
MATCH (origin:Airport)-
[c:CONNECTED_TO]->(destination:Airport)
WHERE destination.code = 'LAS'
RETURN origin, destination, c
----

*Modify and execute this query to use the new model and compare your results for this query profile after the refactoring*

== Exercise 4.5:  Profile our first query that finds all flights that land in Las Vegas. (Solution)

*Modify and execute this query to use the new model and compare your results for this query profile after the refactoring*

[source,cypher]
----
PROFILE
MATCH (f:Flight)
WHERE f.flightId ENDS WITH 'LAS'
RETURN f.flightId
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex4_Profile1.png[Ex4_Profile1,width=150]

{nbsp} +

The original query had 285 db hits. The revised query with the refactored model is better with 49 db hits.

== Exercise 4.6:  Profile our second query that finds all flights for airline 'WN' with flight number '1016'. (Instructions)

The previous query was:

[source,cypher]
----
PROFILE
MATCH  (origin:Airport)-[connection:CONNECTED_TO]->(destination:Airport)
WHERE connection.airline = 'WN' AND connection.flightNumber = '1016'
RETURN origin.code, destination.code, connection.date, connection.departure, connection.arrival
----

*Modify and execute this query to use the new model and compare your results for this query profile after the refactoring*

== Exercise 4.6:  Profile our second query that finds all flights for airline 'WN' with flight number '1016'. (Solution)

*Modify and execute this query to use the new model and compare your results for this query profile after the refactoring*

[source,cypher]
----
PROFILE
MATCH (origin)<-[:ORIGINATES_FROM]-(flight:Flight)-
      [:LANDS_IN]->(destination)
WHERE flight.airline = 'WN' AND
      flight.number = '1016' RETURN origin, destination, flight
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex4_Profile2.png[Ex4_Profile2,width=150]

{nbsp} +

The original query had 5379 db hits. The revised query with the refactored model is better with 40 db hits.

== Exercise 4.7:  Delete the `CONNECTED_TO` relationship from the graph. (Instructions)

*Write and execute the Cypher statement to remove the `CONNECTED_TO` relationships from the graph.*

== Exercise 4.7:  Delete the `CONNECTED_TO` relationship from the graph. (Solution)

*Write and execute the Cypher statement to remove the `CONNECTED_TO` relationships from the graph.*


[source,cypher]
----
MATCH ()-[connection:CONNECTED_TO]->()
DELETE connection
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex4_DeleteRelationships.png[Ex4_DeleteRelationships,width=300]

Deleting unnecessary relationships in the graph is always a good practice since relationships take up space.

== Exercise 4: Creating the `Flight` node from the `CONNECTED_TO` relationships  (Summary)

In this exercise, you refactored the nodes and relationships in the graph for a  model and implementation that performs better.
You added indexes and constraints and created `Flight` nodes from the `CONNECTED_TO` relationships.
You also added the `ORIGINATES_FROM` and `LANDS_IN` relationships between `Flight` nodes and `Airport` nodes.
You profiled queries to confirm that the refactoring improved query performance.
Finally, you deleted the `CONNECTED_TO` relationship.


pass:a[<a play-topic='{guides}/05.html'>Continue to Exercise 5</a>]
