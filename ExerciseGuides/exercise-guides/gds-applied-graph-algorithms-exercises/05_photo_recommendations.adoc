= Photo recommendations

== Photo recommendations

In this section we'll learn how to use the Label Propagation algorithm to cluster similar photos.

== Exploring photo tags

The photos have been run through Google's Image Labeling service and those labels are connected to photos by the `HAS_LABEL` relationship.
We can run the following query to explore this part of the graph:

[source, cypher]
----
MATCH path = (p:Photo)-[:HAS_LABEL]->(label)
RETURN path
LIMIT 25
----

== Similar photos

We want to work out which photos are similar to each other based on the labels they've been tagged with.
We can do this using the https://neo4j.com/docs/graph-data-science/current/algorithms/node-similarity/#algorithms-node-similarity-examples[Node Similarity algorithm^].

Update the following query to:

* Compute similarity between photos based on the labels they have (Hint: you'll need to use the `HAS_LABEL` relationship)
* Find the top 3 most similar photos (Hint: look at the `topK` parameter)
* Store the results in Neo4j (Hint: look at the `writeRelationshipType` and `writeProperty` parameters)

[source, cypher]
----
CALL gds.nodeSimilarity.write({
    similarityCutoff: 0.9, nodeProjection:['Photo','Label'],
    // Add the missing configuration

YIELD nodesCompared, relationshipsWritten, similarityDistribution
RETURN nodesCompared, relationshipsWritten, similarityDistribution
----

== Answer: Similar Photos

This is what the query should look like:

[source, cypher]
----
CALL gds.nodeSimilarity.write({topK: 3,
    similarityCutoff: 0.9, nodeProjection:['Photo','Label'],
    relationshipProjection:'HAS_LABEL', writeProperty:'score',
    writeRelationshipType:'SIMILAR_PHOTO'})

YIELD nodesCompared, relationshipsWritten, similarityDistribution
RETURN nodesCompared, relationshipsWritten, similarityDistribution
----

== Similar photos

Now let's have a look at the relationships created by the algorithm.
Run the following query:

[source, cypher]
----
MATCH path = (p1:Photo)-[r:SIMILAR_PHOTO]->(p2:Photo)
RETURN path
LIMIT 20
----

== Clustering photos

Now we're going to cluster photos based on the `SIMILAR_PHOTO` relationships.
Project the `SIMILAR_PHOTO` relationships as undirected.
Update the following query to do this:

[source, cypher]
----
CALL gds.labelPropagation.write({
    nodeProjection:<nodel-label>, 
    relationshipProjection:<relationship-type>, 
    writeProperty:'partition'})
----

== Answer: Clustering photos

This is what the query should look like:

[source, cypher]
----
CALL gds.labelPropagation.write({
   writeProperty:"partition",
   nodeProjection:"Photo",
   relationshipProjection:{
      SIMILAR_PHOTO:{
         type:"SIMILAR_PHOTO",
         orientation:"UNDIRECTED"
      }
   }
})
----

== Explore photo clusters

Let's see how many photos we have in each cluster.
We can run the following query to compute this:

[source, cypher]
----
MATCH (p:Photo)
RETURN p.partition AS partition, count(*) AS count
ORDER BY count DESC
LIMIT 10
----

Now let's go back to our application and use these clusters to make business recommendations.
