= Exercise 1
:icons: font

== Exercise 1: Traversing the graph (Preparations)

The database you start with should contain no nodes or relationships.

This is what you should see when you click the database icon image:{guides}/img/database-icon.png[].

image::{guides}/img/Empty_database.png[Empty_database,width=150,role=left]

*If your database is not empty, you can run the Cypher below to reset it*:

[source, cypher]
----
MATCH (n) DETACH DELETE n
----

*Verify that your Neo4j Browser session has access to the APOC library by executing the Cypher below*:

[source, cypher]
----
CALL dbms.procedures()
YIELD name
WHERE name STARTS WITH 'apoc.'
RETURN name ORDER BY name ASC
----

If this code does not return the list of APOC procedures, then you must ensure that the APOC library is available by installing the plugin (Neo4j Desktop) and restarting the database.

*Load the graph with the movie data by executing this Cypher code:*

[source, cypher]
----
include::scripts/initialDatabase.cypher[]
----

This is what you should see when you click the database icon image:{guides}/img/database-icon.png[].

image::{guides}/img/InitialDatabase.png[InitialDatabase,200,200, role=left]


== Exercise 1: Traversing the graph (Overview)

In this exercise, you will implement a simple model for people in a company and their skills and answer some questions.

* *Exercise 1.1*: Load the database with Person, Company, and Skill nodes.
* *Exercise 1.2*: Where does Ian work and what are  his skills?
* *Exercise 1.3:* Where does Sarah work and what are her skills?
* *Exercise 1.4:* What people in Ian's company have similar skills as Ian?
* *Exercise 1.5:* Modify Ian's skills.
* *Exercise 1.6:* What are Ian's skills?
* *Exercise 1.7:* What people in Ian's company have similar skill as Ian?


Go to the next page to start this exercise.

== Exercise 1.1: Load the database with Person, Company, and Skill nodes (Instructions)

Here is the data model you will work with in this exercise:

[.thumb]
image::{guides}/img/FirstGraphModel-Ex1.png[FirstGraphModel-Ex1,width=400]

*Execute this Cypher code to load the database for this model*:

[source, cypher]
----
CREATE
(ben:Person{name:'Ben'}),
(charlie:Person{name:'Charlie'}),
(lucy:Person{name:'Lucy'}),
(ian:Person{name:'Ian'}),
(sarah:Person{name:'Sarah'}),
(emily:Person{name:'Emily'}),
(gordon:Person{name:'Gordon'}),
(kate:Person{name:'Kate'}),
(acme:Company{name:'Acme, Inc'}),
(startup:Company{name:'Startup, Ltd'}),
(neo4j:Skill{name:'Neo4j'}),
(rest:Skill{name:'REST'}),
(dotNet:Skill{name:'DotNet'}),
(ruby:Skill{name:'Ruby'}),
(sql:Skill{name:'SQL'}),
(architecture:Skill{name:'Architecture'}),
(java:Skill{name:'Java'}),
(python:Skill{name:'Python'}),
(javascript:Skill{name:'Javascript'}),
(clojure:Skill{name:'Clojure'}),
(ben)-[:WORKS_FOR]->(acme),
(charlie)-[:WORKS_FOR]->(acme),
(lucy)-[:WORKS_FOR]->(acme),
(ian)-[:WORKS_FOR]->(acme),
(sarah)-[:WORKS_FOR]->(startup),
(emily)-[:WORKS_FOR]->(startup),
(gordon)-[:WORKS_FOR]->(startup),
(kate)-[:WORKS_FOR]->(startup),
(ben)-[:HAS_SKILL{level:1}]->(neo4j),
(ben)-[:HAS_SKILL{level:3}]->(rest),
(charlie)-[:HAS_SKILL{level:2}]->(neo4j),
(charlie)-[:HAS_SKILL{level:1}]->(javascript),
(charlie)-[:HAS_SKILL{level:1}]->(sql),
(lucy)-[:HAS_SKILL{level:3}]->(dotNet),
(lucy)-[:HAS_SKILL{level:2}]->(architecture),
(lucy)-[:HAS_SKILL{level:1}]->(python),
(ian)-[:HAS_SKILL{level:2}]->(java),
(ian)-[:HAS_SKILL{level:3}]->(neo4j),
(ian)-[:HAS_SKILL{level:2}]->(rest),
(sarah)-[:HAS_SKILL{level:1}]->(neo4j),
(sarah)-[:HAS_SKILL{level:1}]->(java),
(sarah)-[:HAS_SKILL{level:1}]->(rest),
(sarah)-[:HAS_SKILL{level:1}]->(clojure),
(emily)-[:HAS_SKILL{level:1}]->(neo4j),
(emily)-[:HAS_SKILL{level:1}]->(dotNet),
(emily)-[:HAS_SKILL{level:1}]->(python),
(gordon)-[:HAS_SKILL{level:1}]->(dotNet),
(gordon)-[:HAS_SKILL{level:1}]->(ruby),
(kate)-[:HAS_SKILL{level:1}]->(architecture),
(kate)-[:HAS_SKILL{level:1}]->(python)
----


== Exercise 1.2: Where does Ian work and what are his skills? (Instructions)

*Write a query to display the company that Ian works for and his skills.*


== Exercise 1.2: Where does Ian work and what are his skills? (Solution)

*Write a query to display the company that Ian works for and his skills.*

[source, cypher]
----
MATCH (c:Company)<-[:WORKS_FOR]-(:Person{name:'Ian'})-[r:HAS_SKILL]->(s:Skill)
RETURN c.name, s.name, r.level
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex1_IanSkills.png[Ex1_IanSkills,width=300]

== Exercise 1.3: Where does Sarah work and what are her skills? (Instructions)

*Write a query to display the company that Sarah works for and her skills.*


== Exercise 1.3: Where does Sarah work and what are her skills? (Solution)

*Write a query to display the company that Sarah works for and her skills.*

[source, cypher]
----
MATCH (c:Company)<-[:WORKS_FOR]-(:Person{name:'Sarah'})-[r:HAS_SKILL]->(s:Skill)
RETURN c.name, s.name, r.level
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex1_SarahSkills.png[Ex1_SarahSkills,width=300]

== Exercise 1.4: What people in Ian's company have similar skills as Ian? (Instructions)

*Write a query to display people in Ian's company that have the same skills as Ian.*


== Exercise 1.4: What people in Ian's company have similar skills as Ian?  (Solution)

*Write a query to display people in Ian's company that have the same skills as Ian.*

[source, cypher]
----
MATCH (company)<-[:WORKS_FOR]-(:Person{name:'Ian'})
                -[:HAS_SKILL]->(skill),
(company)<-[:WORKS_FOR]-(colleague)-[:HAS_SKILL]->(skill)
RETURN colleague.name AS name,
count(skill) AS score,
collect(skill.name) AS skills
ORDER BY score DESC
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex1_PeopleWithIansSkill.png[Ex1_PeopleWithIansSkill,width=300]

== Exercise 1.5: Modify Ian's skills. (Instructions)

*Write Cypher that updates Ian's skills to include Java, Python, REST, and Neo4j.*

*Hint*: Use MERGE to avoid duplication of nodes and relationships.

== Exercise 1.5: Modify Ian's skills.  (Solution)

*Write Cypher that updates Ian's skills to include Java, Python, REST, and Neo4j.*

*Hint*: Use MERGE to avoid duplication of nodes and relationships.

You can use MERGE to update the nodes and relationships to reflect the skills for Ian as follows:

[source, cypher]
----
MERGE (p:Person{name:'Ian'})
MERGE (s1:Skill{name:'Java'})
MERGE (s2:Skill{name:'Python'})
MERGE (s3:Skill{name:'Neo4j'})
MERGE (c)<-[:WORKS_FOR]-(p)
MERGE (p)-[r1:HAS_SKILL]->(s1)
MERGE (p)-[r2:HAS_SKILL]->(s2)
MERGE (p)-[r3:HAS_SKILL]->(s3)
SET r1.level = 2
SET r2.level = 2
SET r3.level = 3
RETURN   p, s1, s2, s3
----

Here we use MERGE to ensure duplicate nodes and relationships are not created. For example, we already have nodes and relationships for Java, Neo4j, and REST so some nodes will not be updated.
The only data that is added to the graph is the relationship for the Python skill.

The result returned should be:

[.thumb]
image::{guides}/img/Ex1_ModifyIanSkills.png[Ex1_ModifyIanSkills,width=300]


== Exercise 1.6: What are Ian's skills? (Instructions)

*Repeat the query to display Ian's skills.*

[source, cypher]
----
MATCH (:Person{name:'Ian'})-[r:HAS_SKILL]->(s:Skill)
RETURN  s.name, r.level
----

You should see a different set of skills since you modified Ian's skills.

The result returned should be:

[.thumb]
image::{guides}/img/Ex1_IanSkills2.png[Ex1_IanSkills2,width=300]

== Exercise 1.7: What people in Ian's company have similar skill as Ian?  (Instructions)

*Repeat the query to display people in Ian's company that have the same skills as Ian.*

[source, cypher]
----
MATCH (company)<-[:WORKS_FOR]-(:Person{name:'Ian'})
                -[:HAS_SKILL]->(skill),
(company)<-[:WORKS_FOR]-(colleague)-[:HAS_SKILL]->(skill)
RETURN colleague.name AS name,
count(skill) AS score,
collect(skill.name) AS skills
ORDER BY score DESC
----

You should see a different result here because you have modified Ian's skills.

The result returned should be:

[.thumb]
image::{guides}/img/Ex1_PeopleWithIansSkills2.png[Ex1_PeopleWithIansSkills2,width=300]


== Exercise 1: Implementing your first model (Summary)


In this exercise, you added `Person`, `Skill`, and `Company` nodes to a graph and added the relationships between the nodes.
This graph is a very simple model to review the basic process for implementing a model by creating or merging nodes and relationships.
In the remaining exercises, you will start with a simple model and evolve it by refactoring the data in the graph.
As you add data to a graph, you will most often use MERGE so that duplicate nodes and relationships are not created in the graph.


pass:a[<a play-topic='{guides}/02.html'>Continue to Exercise 2</a>]
