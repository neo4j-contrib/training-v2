= Exercise 13
:icons: font

== Exercise 13: Using indexes (Preparations)

*1. Verify that your Neo4j Browser session has access to the APOC library by executing the Cypher below*:

[source, cypher]
----
CALL dbms.procedures()
YIELD name
WHERE name STARTS WITH 'apoc.'
RETURN name ORDER BY name ASC
----

If this code does not return the list of APOC procedures, then you must ensure that the APOC library is available by installing the plugin (Neo4j Desktop) and restarting the database as follows:

. Make sure Neo4j Desktop is online.
. In Neo4j Desktop for the project you are working with, click  *Add Plugin*.
. Select the install button for APOC.
. Click the Install button.
. Close the Add Plugin window.
. Start or restart the database.

*2. Ensure that the virtual memory and pagecache settings have been adjusted to be:
dbms.memory.heap.initial_size=8G
dbms.memory.heap.max_size=16G
dbms.memory.pagecache.size=800m*

*3. These exercises assume that you have a fully-loaded movie database with the Genre nodes. Execute this code to reset the database:*

[source, cypher]
----
include::scripts/AfterImportDenormalizedData.cypher[]
----

This is what you should see when you click the database icon image:{guides}/img/database-icon.png[].

image::{guides}/img/AfterImportDenormalizedData.png[AfterImportDenormalizedData,200,200, role=left]


== Exercise 12: Optimizing queries (Overview)

In this exercise, you profile queries and optimize them using some best practices.

In this exercise, you will:

* *Exercise 12.1*: Reduce rows processed by collecting intermediate results during the query.
* *Exercise 12.2*: Reduce cardinalities for variable-length paths.
* *Exercise 12.3*: Improve a query that returns unrelated data.
* *Exercise 12.4*: Rewrite a query to collect data using pattern comprehension.
* *Exercise 12.5*: Rewrite a query that limits results.
* *Exercise 12.6*: Defer property access.

Go to the next page to start this exercise.

== Exercise 12.1: Reduce rows processed by collecting intermediate results during the query. (Instructions)

*Here is a query that retrieves the genres and directors for a movie. Execute this query and note how many rows are processed*

[source, cypher]
----
PROFILE MATCH (movie:Movie {title:'Up'})-[:IS_GENRE]-(genre)
MATCH (movie)<-[:DIRECTED]-(director)
RETURN genre, director, movie
----

The profile when executing this code should be:

[.thumb]
image::{guides}/img/EX12.1.png[EX12.1,width=300]

The result of executing this code should be:

[.thumb]
image::{guides}/img/EX12.1B.png[EX12.1B,width=300]

*Rewrite this query to reduce the number of rows processed by collection results during the query.*

== Exercise 12.1: Reduce rows processed by collecting intermediate results during the query. (Solution)

*Here is a query that retrieves the genres and directors for a movie. Execute this query and note how many rows are processed*

[source, cypher]
----
PROFILE MATCH (movie:Movie {title:'Up'})-[:IS_GENRE]-(genre)
MATCH (movie)<-[:DIRECTED]-(director)
RETURN genre, director, movie
----

The profile when executing this code should be:

[.thumb]
image::{guides}/img/EX12.1.png[EX12.1,width=300]

The result of executing this code should be:

[.thumb]
image::{guides}/img/EX12.1B.png[EX12.1B,width=300]

*Rewrite this query to reduce the number of rows processed by collection results during the query.*

Here is the rewritten code:

[source, cypher]
----
PROFILE MATCH (movie:Movie {title:'Up'})-[:IS_GENRE]->(g)
WITH movie, collect(g) as genres
MATCH (movie)<-[:DIRECTED]-(director)
WITH movie, genres, collect(director) as directors
RETURN genres, movie, directors
----

The profile when executing this code should show fewer rows processed:

[.thumb]
image::{guides}/img/EX12.1C.png[EX12.1C,width=300]

The result of executing this code should be:

[.thumb]
image::{guides}/img/EX12.1D.png[EX12.1D,width=300]

== Exercise 12.2: Reduce cardinalities for variable-length paths. (Instructions)

*Execute this Cypher code to retrieve the titles of all movies that are up to 5 hops away from Tom Hanks.*

[source, cypher]
----
PROFILE
MATCH (:Person{name:'Tom Hanks'})-[:ACTED_IN*..5]-(movie)
RETURN movie.title
----

*Modify this query to reduce the number of rows processed.*

== Exercise 12.2: Reduce cardinalities for variable-length paths.  (Solution)

*Execute this Cypher code to retrieve the titles of all movies that are up to 5 hops away from Tom Hanks.*

[source, cypher]
----
PROFILE
MATCH (:Person{name:'Tom Hanks'})-[:ACTED_IN*..5]-(movie)
RETURN movie.title
----

The result of executing this code should be:

[.thumb]
image::{guides}/img/EX12.2.png[EX12.2,width=300]

*Modify this query to reduce the number of rows processed.*

Here is the code:

[source, cypher]
----
PROFILE
MATCH (:Person{name:'Tom Hanks'})-[:ACTED_IN*..5]-(movie)
WITH DISTINCT movie
RETURN movie.title
----

The result of executing this code should be:

[.thumb]
image::{guides}/img/EX12.2B.png[EX12.2B,width=300]

== Exercise 12.3: Improve a query that returns unrelated data. (Instructions)

*Execute this Cypher code to retrieve the titles of all movies  released before 1950 and people born before 1920.*

[source, cypher]
----
PROFILE MATCH (p:Person), (m:Movie)
WHERE  p.born < 1920 AND m.releaseYear < 1950
RETURN p, m
----

*What is wrong with this query? Modify this query to optimize it.*

== Exercise 12.3: Improve a query that returns unrelated data.   (Solution)

*Execute this Cypher code to retrieve the titles of all movies released before 1920 and people born before 1920.*

[source, cypher]
----
PROFILE MATCH (p:Person), (m:Movie)
WHERE  p.born < 1920 AND m.releaseYear < 1920
WITH collect(p.name) as people, collect(m.title) as movies
RETURN people, movies
----

*What is wrong with this query? Modify this query to optimize it.*

The result of executing this query should be the following where we see a cartesian product:

[.thumb]
image::{guides}/img/EX12.3.png[EX12.3,width=300]


Here is the improved code:

[source, cypher]
----
PROFILE MATCH (p:Person)
WHERE  p.born < 1920
WITH collect(p.name) AS people
MATCH (m:Movie)
WHERE  m.releaseYear < 1920
WITH collect(m.title) as movies, people
RETURN people, movies
----

The result of executing this code should be:

[.thumb]
image::{guides}/img/EX12.3B.png[EX12.3B,width=300]

== Exercise 12.4: Rewrite a query to collect data using pattern comprehension. (Instructions)

*Execute this Cypher code to retrieve the titles of of movies that an young actor (born after 1990) has acted in in the three year period of 2010 to 2012.*

[source, cypher]
----
PROFILE MATCH (a:Actor)-[:ACTED_IN]->(m:Movie)
WHERE 2010 <= m.releaseYear <= 2012 AND a.born > 1990
RETURN a.name as actor, a.born as born,
collect(DISTINCT m.title) AS titles ORDER BY actor
----

*Rewrite this query to collect data using pattern comprehension to collect the titles.*

== Exercise 12.4: Rewrite a query to collect data using pattern comprehension.  (Solution)

*Execute this Cypher code to retrieve the titles of of movies that an young actor (born after 1990) has acted in in the three year period of 2010 to 2012.*

[source, cypher]
----
PROFILE MATCH (a:Actor)-[:ACTED_IN]->(m:Movie)
WHERE 2010 <= m.releaseYear <= 2012 AND a.born > 1990
RETURN a.name as actor, a.born as born,
collect(DISTINCT m.title) AS titles ORDER BY actor
----

The result of executing this query should be the following:

[.thumb]
image::{guides}/img/EX12.4.png[EX12.4,width=300]


*Rewrite this query to collect data using pattern comprehension to collect the titles.*

Here is the code:

[source, cypher]
----
PROFILE MATCH (a:Actor)
WHERE a.born > 1990
RETURN a.name AS actor,  a.born AS born,
[(a)-->(x) WHERE 2010 <= x.releaseYear <= 2012 | x.title] AS titles ORDER BY actor
----

The result of executing this query should be the following:

[.thumb]
image::{guides}/img/EX12.4B.png[EX12.4B,width=300]


If you look at the rows returned with this code, you will notice that empty titles collections are produced which is not the case in the previous query.

== Exercise 12.5: Rewrite a query that limits results. (Instructions)

*Execute this Cypher code to return a movie released in 2015 along with its actors.*

[source, cypher]
----
PROFILE MATCH (movie:Movie)
WHERE movie.releaseYear = 2015
OPTIONAL MATCH (movie)<-[:ACTED_IN]-(actor)
WITH movie, collect(actor.name) as actors
RETURN movie.title, actors LIMIT 1
----

*Rewrite this query with a small change to perform better.*

== Exercise 12.5: Rewrite a query that limits results.  (Solution)

*Execute this Cypher code to return a movie released in 2015 along with its actors.*

[source, cypher]
----
PROFILE MATCH (movie:Movie)
WHERE movie.releaseYear = 2015
OPTIONAL MATCH (movie)<-[:ACTED_IN]-(actor)
WITH movie, collect(actor.name) as actors
RETURN movie.title, actors LIMIT 1
----

Here is the profile when executing this query:

[.thumb]
image::{guides}/img/EX12.5.png[EX12.5,width=300]

*Rewrite this query with a small change to perform better.*

Here is the modified query:

[source, cypher]
----
PROFILE MATCH (movie:Movie)
WHERE movie.releaseYear = 2015
WITH movie LIMIT 1
OPTIONAL MATCH (movie)<-[:ACTED_IN]-(actor)
WITH movie, collect(actor.name) as actors
RETURN movie.title, actors
----

The profile for executing this rewritten query should be the following:

[.thumb]
image::{guides}/img/EX12.5B.png[EX12.5B,width=300]

== Exercise 12.6: Defer property access. (Instructions)

*Execute this Cypher code to return the top ten movies with the largest casts.*

[source, cypher]
----
PROFILE
MATCH (a)-[:ACTED_IN]->(m:Movie)
RETURN m.title as title, count(a) as numActors
ORDER BY numActors DESC LIMIT 10
----

*Rewrite this query with a small change to perform better by deferring property access.*

== Exercise 12.6: Defer property access.  (Solution)

*Execute this Cypher code to return the top ten movies with the largest casts.*

[source, cypher]
----
PROFILE
MATCH (a)-[:ACTED_IN]->(m:Movie)
RETURN m.title as title, count(a) as numActors
ORDER BY numActors DESC LIMIT 10
----

Here is the profile when executing this query:

[.thumb]
image::{guides}/img/EX12.6.png[EX12.6,width=300]

*Rewrite this query with a small change to perform better by deferring property access.*

Here is the modified query:

[source, cypher]
----
PROFILE
MATCH (a)-[:ACTED_IN]->(m:Movie)
WITH m, count(a) as numActors ORDER by numActors DESC LIMIT 10
RETURN m.title, numActors
----

The profile for executing this rewritten query should be the following:

[.thumb]
image::{guides}/img/EX12.6B.png[EX12.6B,width=300]


== Exercise 12: Taking it further

. Use parameters in some of the queries you have worked on in this exercise.
. In Exercise 12.4, the revised query was:

[source, cypher]
----
PROFILE MATCH (a:Actor)
WHERE a.born > 1990
RETURN a.name AS actor,  a.born AS born,
[(a)-->(x) WHERE 2010 <= x.releaseYear <= 2012 | x.title] AS titles ORDER BY actor
----

Modify this query so that empty collections are not returned.

== Exercise 11: Preparing for query tuning   (Summary)

In this exercise, you modified queries to perform better.
pass:a[<a play-topic='{guides}/13.html'>Continue to Exercise 132</a>]
