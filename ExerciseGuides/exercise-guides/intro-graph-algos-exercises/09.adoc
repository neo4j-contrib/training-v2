= Exercise 9
:icons: font

== Exercise 9: Pathfinding (Preparations)

The database you start with should contain all of the data you loaded in the setup for this course.

This is what you should see when you click the database icon image:{guides}/img/database-icon.png[].

image::{guides}/img/LoadedDatabase.png[LoadedDatabase,width=300]

{nbsp} +

If you do not see this in your Neo4j Browser, you will need to perform the setup steps again.

== Exercise 9: Pathfinding (Overview)

There are many ways that search and path finding can be implemented in Neo4j.
In this exercise you will gain experience with a variety of Pathfinding algorithms.

In this exercise, you will execute Pathfinding algorithms for the European Roads dataset:

* *Exercise 9.1*: Find the shortest path with Cypher.
* *Exercise 9.2*: Find the shortest weighted path with the Shortest Path algorithm.
* *Exercise 9.3*: Find the shortest paths using Ken's K-shortest Path algorithm.
* *Exercise 9.4*: Find the shortest path between two nodes using the Single Source Shortest Path algorithm.
* *Exercise 9.5*: Find the Minimum Spanning Tree path from a node.
* *Exercise 9.6*: Use the Random Walk algorithm.

Go to the next page to start this exercise.

== Exercise 9.1: Find the shortest path with Cypher. (Instructions/Solution)

You will start with a query that finds the shortest path in terms of hops (number of relationships) between two nodes.
Execute the following query uses Neo4j’s built in shortest path function to find a path between Århus and Roma:

[source, cypher]
----
MATCH p=shortestPath((start:Place {name: "Århus"})-[rels:EROAD*]-(end:Place {name: "Roma"}))
RETURN [place in nodes(p) | place.name][1..-1] AS journey,
       length(nodes(p)[1..-1]) AS intermediatePlaces,
       reduce(s = 0, r in rels | s + r.distance) AS total_distance
----

In this query, we also calculate the distance by summing together the distance property on the relationships in the path.
In this case the path between Århus and Roma is 3148.

Modify this code to specify two different places to find the shortest path between them.

== Exercise 9.2: Find the shortest weighted path with the Shortest Path algorithm. (Instructions)

The previous query finds us the path that goes through the fewest other cities, but it’s not necessarily the shortest in terms of distance.

The shortestPath algorithm takes into account the weight of the relationships to determine the shortest path.

Write code to call to `algo.shortestPath.stream` using the following guidelines:

* Use "Århus" and the starting node and "Roma" as the ending node. (You will need to find them in the graph before you run the algorithm)
* Specify *distance* as the property to use for the weight.
* Return the node name and the cost

== Exercise 9.2: Find the shortest weighted path with the Shortest Path algorithm. (Solution)

The previous query finds us the path that goes through the fewest other cities, but it’s not necessarily the shortest in terms of distance.

The shortestPath algorithm takes into account the weight of the relationships to determine the shortest path.

Write code to call to `algo.shortestPath.stream` using the following guidelines:

* Use "Århus" and the starting node and "Roma" as the ending node. (You will need to find them in the graph before you run the algorithm)
* Specify *distance* as the property to use for the weight.
* Return the node name and the cost

Here is the solution code:

[source, cypher]
----
MATCH (aarhus:Place {name: "Århus"}), (rome:Place {name: "Roma"})
CALL algo.shortestPath.stream(aarhus, rome, "distance")
YIELD nodeId, cost
MATCH (n) WHERE id(n) = nodeId
return n.name, cost
----

The results returned should look like this:

[.thumb]
image::{guides}/img/EX9.2.png[EX9.2,width=300]

With this algorithm, we see that the shortest path here used the distances between nodes to indeed find the shortest part of 2147.


== Exercise 9.3: Find the shortest paths using Ken's K-shortest Path algorithm. (Instructions)

What if you want to find the 2nd shortest path, or 3rd shortest path between two nodes?
You can use the Yen’s algorithm to do this.

The following query finds the top 5 shortest paths between Berlin and Roma. Run this code:

[source, cypher]
----
MATCH (start:Place {name:"Berlin"}), (end:Place {name:"Roma"})
CALL algo.kShortestPaths.stream(start, end, 5, 'distance')
YIELD index, nodeIds, path, costs
RETURN index,
       [node in algo.getNodesById(nodeIds[1..-1]) | node.name] AS via,
       reduce(acc=0.0, cost in costs | acc + cost) AS totalCost
----

Modify this code to find the three shortest paths between London and Berlin.

== Exercise 9.3: Find the shortest paths using Ken's K-shortest Path algorithm. (Solution)

What you want to find the 2nd shortest path, or 3rd shortest path between two nodes?
You can use the Yen’s algorithm to do this.

The following query finds the top 5 shortest paths between Berlin and Roma. Run this code:

[source, cypher]
----
MATCH (start:Place {name:"Berlin"}), (end:Place {name:"Roma"})
CALL algo.kShortestPaths.stream(start, end, 5, 'distance')
YIELD index, nodeIds, path, costs
RETURN index,
       [node in algo.getNodesById(nodeIds[1..-1]) | node.name] AS via,
       reduce(acc=0.0, cost in costs | acc + cost) AS totalCost
----

Modify this code to find the three shortest paths between London and Berlin.

Here is the solution code:

[source, cypher]
----
MATCH (start:Place {name:"London"}), (end:Place {name:"Berlin"})
CALL algo.kShortestPaths.stream(start, end, 3, 'distance')
YIELD index, nodeIds, path, costs
RETURN index,
       [node in algo.getNodesById(nodeIds[1..-1]) | node.name] AS via,
       reduce(acc=0.0, cost in costs | acc + cost) AS totalCost
----

The results returned should look like this:

[.thumb]
image::{guides}/img/EX9.3.png[EX9.3,width=300]

== Exercise 9.4: Find the shortest path between two nodes using the Single Source Shortest Path algorithm. (Instructions/Solution)

The Single Source Shortest Path algorithm finds the shortest path from a specified node to all other nodes.

Run this code to find the shortest path from London to all other reachable locations:

[source, cypher]
----
MATCH (n:Place {name:"London"})
CALL algo.shortestPath.deltaStepping.stream(n, "distance", 1.0)
YIELD nodeId, distance WHERE algo.isFinite(distance)
RETURN algo.getNodeById(nodeId).name AS destination, distance
ORDER BY distance
----

Modify this code to specify a different starting location.

== Exercise 9.5:  Find the Minimum Spanning Tree path from a node. (Instructions/Solution)

The Minimum Spanning Tree algorithm starts from a given node, and finds all its reachable nodes and the set of relationships that connect the nodes together with the minimum possible weight.

The following code calculates this tree starting from Amsterdam, and stores relationships of type `MINST` between the nodes in the tree.
Run this code to create the Minimum Spanning Tree relationships:

[source, cypher]
----
MATCH (n:Place {name:"Amsterdam"})
CALL algo.spanningTree.minimum("Place", "EROAD", "distance", id(n),
  {write:true, writeProperty:"MINST"})
YIELD loadMillis, computeMillis, writeMillis, effectiveNodeCount
RETURN loadMillis, computeMillis, writeMillis, effectiveNodeCount
----

To see the output of the algorithm, execute the following query:

[source, cypher]
----
MATCH path = (p:Place {name:"Amsterdam"})-[:MINST*]-(end)
RETURN [n in nodes(path) | n.name]
----

== Exercise 9.6:   Use the Random Walk algorithm. (Instructions/Solution)

The Random Walk algorithm starts from a node (which you can choose to provide), chooses a neighbor to navigate to at random, and then does the same from that node, keeping the resulting path in a list.

Run the following code to generate random walks starting from Goch:

[source, cypher]
----
MATCH (source:Place {name: "Goch"})
CALL algo.randomWalk.stream(id(source), 10, 5)
YIELD nodeIds
RETURN [place in algo.getNodesById(nodeIds) | place.name] AS places
----

You can use this to try and find parts of the network that are not that resilient e.g. maybe there is only one road in or out of a city.

Run the following code to generate 50,000 random walks of length 10 starting from Goch:

[source, cypher]
----
MATCH (source:Place {name: "Goch"})
CALL algo.randomWalk.stream(id(source), 10, 50000)
YIELD nodeIds
RETURN [place in algo.getNodesById(nodeIds) | place.name] AS places, count(*) AS times
ORDER BY times DESC
----

There are some combinations that show up more than we might expect at random.
Execute the following query to explore the graph around one of them:

[source, cypher]
----
MATCH (p1:Place {name: "Nijmegen"})
MATCH (p2:Place {name: "Goch"})
MATCH path = (neighbor1)-[:EROAD]-(p1)-[:EROAD]-(p2)-[:EROAD]-(neighbor2)
RETURN path
----

You can see that there is only one road in and out of these places, which is why the random walk often goes back and forwards between these nodes.

== Exercise 9: Taking it further

Try some Pathfinding analysis on the Yelp dataset.

== Exercise 9: Pathfinding (Summary)

There are many ways that search and path finding can be implemented in Neo4j.
In this exercise you gained experience with a variety of Pathfinding algorithms using the European Roads dataset.

pass:a[<a play-topic='{guides}/10.html'>Continue to Exercise 10</a>]
