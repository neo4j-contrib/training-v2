= Exercise 5
== Exercise 5: Controlling query processing (Preparations)

Make sure you have the Movie database, which contains 171 nodes and 253 relationships.

This is what you should see when you click the database icon:

image::{guides}/img/InitialDatabase.png[InitialDatabase,200,200, role=left]

If your database does not have this number of nodes and relationships, you can use the script below to reset it:

[source, cypher]
----
include::scripts/initialDatabase.cypher[]
----


== Exercise 5: Controlling query processing (Overview)

In the previous exercise, you have filtered your queries using the `WHERE` clause of a Cypher query.

In this exercise, you will write queries that filter the results that are returned using a `WHERE` clause as well as performing some additional processing during the query.
First, you will write a query where multiple patterns are required.
Then you write a query that uses a variable length pattern to retrieve data.
Next, you will write a query that retrieves data with optional results.
Then you will collect results during a query to build a list.
Next, you count retrievals during the query.
Finally, you will perform intermediate processing during the query using a `WITH` clause.

* *Exercise 5.1*: Retrieve data using multiple `MATCH` patterns.
* *Exercise 5.2*: Retrieve all nodes that the person named _James Thompson_ directly has the _FOLLOWS_ relationship in either direction.
* *Exercise 5.3*: Modify the query to retrieve nodes that are exactly three hops away.
* *Exercise 5.4*: Modify the query to retrieve nodes that are one and two hops away.
* *Exercise 5.5*: Modify the query to retrieve all nodes that are connected to _James Thompson_ by a _Follows_ relationship no matter how many hops are required.
* *Exercise 5.6*: Specify optional data to be retrieved during the query.
* *Exercise 5.7*: Retrieve actors and the movies they have acted in by collecting a list.
* *Exercise 5.8*: Retrieve all movies that _Tom Cruise_ has acted in and the co-actors that acted in the same movie by collecting a list.
* *Exercise 5.9*: Retrieve all people who reviewed a movie, returning the list of reviewers and how many reviewers reviewed the movie.
* *Exercise 5.10*: Retrieve all directors, their movies, and people who acted in the movies, returning the name of the director, the number of actors the director has worked with, and the list of actors.
* *Exercise 5.11*: Retrieve the actors who have acted in exactly five movies.
* *Exercise 5.12*: Retrieve the movies that have at least 2 directors, and optionally the names of people who reviewed the movies.

Go to the next page to start this exercise.

== Exercise 5.1: Retrieve data using multiple `MATCH` patterns (Instructions)

*Write a Cypher query that retrieves all movies that _Gene Hackman_ has acted it, along with the directors of the movies. In addition, retrieve the actors that acted in the same movies as _Gene Hackman_. Return the name of the movie, the name of the director, and the names of actors that worked with _Gene Hackman_.*


== Exercise 5.1: Retrieve data using multiple `MATCH` patterns (Solution)

*Write a Cypher query that retrieves all movies that _Gene Hackman_ has acted it, along with the directors of the movies. In addition, retrieve the actors that acted in the same movies as _Gene Hackman_. Return the name of the movie, the name of the director, and the names of actors that worked with _Gene Hackman_.*

[source, cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(d:Person),
      (a2:Person)-[:ACTED_IN]->(m)
WHERE a.name = 'Gene Hackman'
RETURN m.title as movie, d.name AS director , a2.name AS `co-actors`
----

[.thumb]
image::{guides}/img/GeneHackmanMoviesWithDirectorAndCoActors.png[GeneHackmanMoviesWithDirectorAndCoActors,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise 5.2: Retrieve all nodes that the person named _James Thompson_ directly has the _FOLLOWS_ relationship in either direction (Instructions)

Next, you will perform queries for data that is a variable number of hops away.

Write this Cypher query to explore the graph engine's behavior with varying length paths for the _FOLLOWS_ relationship in the _Movie_ graph:

*Retrieve all nodes that the person named _James Thompson_ directly has the _FOLLOWS_ relationship in either direction.*

== Exercise 5.2: Retrieve all nodes that the person named _James Thompson_ directly has the _FOLLOWS_ relationship in either direction (Solution)

*Retrieve all nodes that the person named _James Thompson_ directly has the _FOLLOWS_ relationship in either direction.*

[source, cypher]
----
MATCH (p1:Person)-[:FOLLOWS]-(p2:Person)
WHERE p1.name = 'James Thompson'
RETURN p1, p2
----

[.thumb]
image::{guides}/img/JamesThompsonFollows.png[JamesThompsonFollows,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise 5.3: Modify the query to retrieve nodes that are exactly three hops away (Instructions)

*Modify the query to retrieve nodes that are exactly three hops away.*

== Exercise 5.3: Modify the query to retrieve nodes that are exactly three hops away (Solution)

*Modify the query to retrieve nodes that are exactly three hops away.*

[source, cypher]
----
MATCH (p1:Person)-[:FOLLOWS*3]-(p2:Person)
WHERE p1.name = 'James Thompson'
RETURN p1, p2
----

[.thumb]
image::{guides}/img/JamesThompson3Hops.png[JamesThompson3Hops,width=400,float="right"]

The result returned should be:

Notice here that the relationships between the nodes are not displayed because _James Thompson_ is not connected to _Paul Blythe_.

++++
<div style="clear: both;"></div>
++++


== Exercise 5.4: Modify the query to retrieve nodes that are one and two hops away (Instructions)

*Modify the query to retrieve nodes that are one and two hops away.*

== Exercise 5.4: Modify the query to retrieve nodes that are one and two hops away (Solution)

*Modify the query to retrieve nodes that are one and two hops away.*

[source, cypher]
----
MATCH (p1:Person)-[:FOLLOWS*1..2]-(p2:Person)
WHERE p1.name = 'James Thompson'
RETURN p1, p2
----

[.thumb]
image::{guides}/img/JamesThompson1And2Hops.png[JamesThompson1And2Hops,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++


== Exercise 5.5: Modify the query to retrieve all nodes that are connected to _James Thompson_ by a _Follows_ relationship no matter how many hops are required (Instructions)

*Modify the query to retrieve all nodes that are connected to _James Thompson_ by a _Follows_ relationship no matter how many hops are required.*


== Exercise 5.5: Modify the query to retrieve all nodes that are connected to _James Thompson_ by a _Follows_ relationship no matter how many hops are required (Solution)

*Modify the query to retrieve all nodes that are connected to _James Thompson_ by a _Follows_ relationship no matter how many hops are required.*

[source, cypher]
----
MATCH (p1:Person)-[:FOLLOWS*]-(p2:Person)
WHERE p1.name = 'James Thompson'
RETURN p1, p2
----

[.thumb]
image::{guides}/img/JamesThompsonAllHops.png[JamesThompsonAllHops,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise 5.6: Specify optional data to be retrieved during the query (Instructions)

*Write a Cypher query to retrieve all people in the graph whose name begins with _Tom_ and optionally retrieve all people named _Tom_ who directed a movie.*

== Exercise 5.6: Specify optional data to be retrieved during the query (Solution)

*Write a Cypher query to retrieve all people in the graph whose name begins with _Tom_ and optionally retrieve all people named _Tom_ who directed a movie.*

[source, cypher]
----
MATCH (p:Person)
WHERE p.name STARTS WITH 'Tom'
OPTIONAL MATCH (p)-[:DIRECTED]->(m:Movie)
RETURN p.name, m.title
----

[.thumb]
image::{guides}/img/OptionalMatch.png[OptionalMatch,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise 5.7: Retrieve actors and the movies they have acted in by collecting a list (Instructions)

Next, you will perform queries where you collect the results into a list.

*Retrieve actors and the movies they have acted in, returning each actor's name and the list of movies they acted in.*


== Exercise 5.7: Retrieve actors and the movies they have acted in by collecting a list (Solution)

*Retrieve actors and the movies they have acted in, returning each actor's name and the list of movies they acted in.*

[source, cypher]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
RETURN p.name as actor, collect(m.title) AS `movie list`
----

[.thumb]
image::{guides}/img/ActorMovies.png[ActorMovies,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise 5.8: Retrieve all movies that _Tom Cruise_ has acted in and the co-actors that acted in the same movie by collecting a list (Instructions)

*Retrieve all movies that _Tom Cruise_ has acted in and the co-actors that acted in the same movie, returning the movie title and the list of co-actors that _Tom Cruise_ worked with.*

== Exercise 5.8: Retrieve all movies that _Tom Cruise_ has acted in and the co-actors that acted in the same movie by collecting a list (Solution)

*Retrieve all movies that _Tom Cruise_ has acted in and the co-actors that acted in the same movie, returning the movie title and the list of co-actors that _Tom Cruise_ worked with.*

[source, cypher]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)<-[:ACTED_IN]-(p2:Person)
WHERE p.name ='Tom Cruise'
RETURN m.title as movie, collect(p2.name) AS `co-actors`
----

[.thumb]
image::{guides}/img/TomCruiseCoActors.png[TomCruiseCoActors,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise 5.9: Retrieve all people who reviewed a movie, returning the list of reviewers and how many reviewers reviewed the movie (Instructions)

Next, you will count results retrieved.

*Retrieve all people who reviewed a movie, returning the list of reviewers and how many reviewers reviewed the movie.*

== Exercise 5.9: Retrieve all people who reviewed a movie, returning the list of reviewers and how many reviewers reviewed the movie (Solution)

*Retrieve all people who reviewed a movie, returning the list of reviewers and how many reviewers reviewed the movie.*

[source, cypher]
----
MATCH (p:Person)-[:REVIEWED]->(m:Movie)
RETURN m.title as movie, count(p) as numReviews, collect(p.name) as reviewers
----

[.thumb]
image::{guides}/img/CountReviews.png[CountReviews,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise 5.10: Retrieve all directors, their movies, and people who acted in the movies, returning the name of the director, the number of actors the director has worked with, and the list of actors (Instructions)

*Retrieve all directors, their movies, and people who acted in the movies, returning the name of the director, the number of actors the director has worked with, and the list of actors.*

== Exercise 5.10: Retrieve all directors, their movies, and people who acted in the movies, returning the name of the director, the number of actors the director has worked with, and the list of actors (Solution)

*Retrieve all directors, their movies, and people who acted in the movies, returning the name of the director, the number of actors the director has worked with, and the list of actors.*

[source, cypher]
----
MATCH (d:Person)-[:DIRECTED]->(m:Movie)<-[:ACTED_IN]-(a:Person)
RETURN d.name AS director, count(a) AS `number actors` , collect(a.name) AS `actors worked with`
----

[.thumb]
image::{guides}/img/CountActorsWhoWorkedWithDirectors.png[CountActorsWhoWorkedWithDirectors,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise 5.11: Retrieve the actors who have acted in exactly five movies (Instructions)

Next, you will write queries that perform some intermediate processing using the `WITH` clause.

*Retrieve the actors who have acted in exactly five movies, returning the name of the actor, and the list of movies for that actor.*

== Exercise 5.11: Retrieve the actors who have acted in exactly five movies (Solution)

*Retrieve the actors who have acted in exactly five movies, returning the name of the actor, and the list of movies for that actor.*

[source, cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WITH  a, count(a) AS numMovies, collect(m.title) AS movies
WHERE numMovies = 5
RETURN a.name, movies
----

[.thumb]
image::{guides}/img/FiveMovies.png[FiveMovies,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise 5.12: Retrieve the movies that have at least 2 directors, and optionally the names of people who reviewed the movies (Instructions)

*Retrieve the movies that have at least 2 directors, and optionally the names of people who reviewed the movies.*

== Exercise 5.12: Retrieve the movies that have at least 2 directors, and optionally the names of people who reviewed the movies (Solution)

*Retrieve the movies that have at least 2 directors, and optionally the names of people who reviewed the movies.*

[source, cypher]
----
MATCH (m:Movie)
WITH m, size((:Person)-[:DIRECTED]->(m)) AS directors
WHERE directors >= 2
OPTIONAL MATCH (p:Person)-[:REVIEWED]->(m)
RETURN  m.title, p.name
----

[.thumb]
image::{guides}/img/TopDirectorsAndReviewers.png[TopDirectorsAndReviewers,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise 5: Controlling query processing (Summary)

In this exercise, you wrote queries that filtered the results that are returned using a `WHERE` clause as well as performing some additional processing during the query.
First, you wrote a query where multiple patterns are required.
Then you wrote a query that uses a variable length pattern to retrieve data.
Next, you wrote a query that retrieves data with optional results.
Then you collected results during a query to build a list.
Next, you counted retrievals during the query.
Finally, you performed intermediate processing during the query using a `WITH` clause.

pass:a[<a play-topic='{guides}/06.html'>Continue to Exercise 6</a>]
