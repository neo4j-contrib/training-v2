= Exercise 7
== Exercise 7: Creating Relationships (Preparations)

Make sure you have the Movie database, which contains 173 nodes and 253 relationships.

This is what you should see when you click the database icon:

image::{guides}/img/AfterExercise6.png[After exercise 6,200,200, role=left]

If your database does not have this number of nodes and relationships, you can use the script below to reset it:

[source, cypher]
----
include::scripts/AfterExercise6.cypher[]
----

== Exercise 7: Creating Relationships (Overview)

In the previous exercise, you created nodes, added and removed both properties and labels from nodes.
The connections between nodes, the relationships, are one of the most important parts of a Neo4j graph.

In this exercise you will create relationships between nodes.
Then you will add properties to relationships.
Finally, you will remove properties from relationships.

* *Exercise 7.1*: Create _ACTED_IN_ relationships.
* *Exercise 7.2*: Create _DIRECTED_ relationships.
* *Exercise 7.3*: Create a _HELPED_ relationship.
* *Exercise 7.4*: Query nodes and new relationships.
* *Exercise 7.5*: Add properties to relationships.
* *Exercise 7.6*: Add a property to the _HELPED_ relationship.
* *Exercise 7.7*: View the current list of property keys in the graph.
* *Exercise 7.8*: View the current schema of the graph.
* *Exercise 7.9*: Retrieve the names and roles for actors.
* *Exercise 7.10*: Retrieve information about any specific relationships.
* *Exercise 7.11*: Modify a property of a relationship.
* *Exercise 7.12*: Remove a property from a relationship.
* *Exercise 7.13*: Confirm that your modifications were made to the graph.

Go to the next page to start this exercise.

== Exercise 7.1: Create _ACTED_IN_ relationships (Instructions)

In the last exercise, you created the node for the movie, _Forrest Gump_ and the person, _Robin Wright_.

*Create the _ACTED_IN_ relationship between the actors, _Robin Wright_, _Tom Hanks_, and _Gary Sinise_ and the movie, _Forrest Gump_.*

== Exercise 7.1: Create _ACTED_IN_ relationships (Solution)

*Create the _ACTED_IN_ relationship between the actors, _Robin Wright_, _Tom Hanks_, and _Gary Sinise_ and the movie, _Forrest Gump_.*

[source, cypher]
----
MATCH (m:Movie) 
WHERE m.title = 'Forrest Gump'
MATCH (p:Person)
WHERE p.name = 'Tom Hanks' OR p.name = 'Robin Wright' OR p.name = 'Gary Sinise'
CREATE (p)-[:ACTED_IN]->(m)
----

The result returned should be:

[.thumb]
image::{guides}/img/CreateThreeRelationships.png[CreateThreeRelationships,width=400]



++++
<div style="clear: both;"></div>
++++

== Exercise 7.2: Create _DIRECTED_ relationships (Instructions)

*Create the _DIRECTED_ relationship between _Robert Zemeckis_ and the movie, _Forrest Gump_.*

== Exercise 7.2: Create _DIRECTED_ relationships (Solution)

*Create the _DIRECTED_ relationship between _Robert Zemeckis_ and the movie, _Forrest Gump_.*

[source, cypher]
----
MATCH (m:Movie) 
WHERE m.title = 'Forrest Gump'
MATCH (p:Person)
WHERE p.name = 'Robert Zemeckis'
CREATE (p)-[:DIRECTED]->(m)
----

The result returned should be:

[.thumb]
image::{guides}/img/AddDirectedRelationship.png[AddDirectedRelationship,width=400]



++++
<div style="clear: both;"></div>
++++

== Exercise 7.3: Create a _HELPED_ relationship (Instructions)

*Create a new relationship, _HELPED_ from _Tom Hanks_ to _Gary Sinise_.*

== Exercise 7.3: Create a _HELPED_ relationship (Solution)

*Create a new relationship, _HELPED_ from _Tom Hanks_ to _Gary Sinise_.*

[source, cypher]
----
MATCH (p1:Person) 
WHERE p1.name = 'Tom Hanks'
MATCH (p2:Person)
WHERE p2.name = 'Gary Sinise'
CREATE (p1)-[:HELPED]->(p2)
----

The result returned should be:

[.thumb]
image::{guides}/img/CreateHELPEDRelationship.png[CreateHELPEDRelationship,width=400]


++++
<div style="clear: both;"></div>
++++

== Exercise 7.4: Query nodes and new relationships (Instructions)

*Write a Cypher query to return all nodes connected to the movie, _Forrest Gump_, along with their relationships.*

== Exercise 7.4: Query nodes and new relationships (Solution)

*Write a Cypher query to return all nodes connected to the movie, _Forrest Gump_, along with their relationships.*

[source, cypher]
----
MATCH (p:Person)-[rel]-(m:Movie)
WHERE m.title = 'Forrest Gump'
RETURN p, rel, m
----

The result returned should be:

[.thumb]
image::{guides}/img/ForrestGumpRelationships.png[ForrestGumpRelationships,width=400]


Notice here that the _HELPED_ relationship is also shown because _Tom Hanks_ and _Gary Sinise_ are connected and we have _Connect result nodes_ selected in our Neo4j Browser settings.

++++
<div style="clear: both;"></div>
++++

== Exercise 7.5: Add properties to relationships (Instructions)

Next, you will add some properties to the relationships that you just created.

*Add the _roles_ property to the three _ACTED_IN_ relationships that you just created to the movie, _Forrest Gump_ using this information:*
*_Tom Hanks_ played the role, _Forrest Gump_.*
*_Robin Wright_ played the role, _Jenny Curran_.*
*_Gary Sinise_ played the role, _Lieutenant Dan Taylor_.*

*Hint:* You can set each relationship using separate `MATCH` clauses.
You can also use a `CASE` clause to set the values.
Look up in the documentation for how to use the `CASE` clause.

== Exercise 7.5: Add properties to relationships (Solution)

*Add the _roles_ property to the three _ACTED_IN_ relationships that you just created to the movie, _Forrest Gump_ using this information:*
*_Tom Hanks_ played the role, _Forrest Gump_.*
*_Robin Wright_ played the role, _Jenny Curran_.*
*_Gary Sinise_ played the role, _Lieutenant Dan Taylor_.*

[source, cypher]
----
MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump'
SET rel.roles = 
CASE p.name
  WHEN 'Tom Hanks' THEN ['Forrest Gump']
  WHEN 'Robin Wright' THEN ['Jenny Curran']
  WHEN 'Gary Sinise' THEN ['Lieutenant Dan Taylor']
END
----

The result returned should be:

[.thumb]
image::{guides}/img/SetPropertiesUsingCase.png[SetPropertiesUsingCase,width=400]



++++
<div style="clear: both;"></div>
++++

== Exercise 7.6: Add a property to the _HELPED_ relationship (Instructions)

*Add a new property, _research_ to the _HELPED_ relationship between _Tom Hanks_ and _Gary Sinise_ and set this property's value to _war history_.*

== Exercise 7.6: Add a property to the _HELPED_ relationship (Solution)

*Add a new property, _research_ to the _HELPED_ relationship between _Tom Hanks_ and _Gary Sinise_ and set this property's value to _war history_.*

[source, cypher]
----
MATCH (p1:Person)-[rel:HELPED]->(p2:Person)
WHERE p1.name = 'Tom Hanks' AND p2.name = 'Gary Sinise'
SET rel.research = 'war history'
----

The result returned should be:

[.thumb]
image::{guides}/img/SetResearchProperty.png[SetResearchProperty,width=400]



++++
<div style="clear: both;"></div>
++++

== Exercise 7.7: View the current list of property keys in the graph (Instructions)

*View the current list of property keys in the graph.*

== Exercise 7.7: View the current list of property keys in the graph (Solution)

*View the current list of property keys in the graph.*

[source, cypher]
----
call db.propertyKeys
----

The result returned should be:

[.thumb]
image::{guides}/img/PropertyKeys-research.png[PropertyKeys-research,width=400]



++++
<div style="clear: both;"></div>
++++

== Exercise 7.8: View the current schema of the graph (Instructions)

*View the current schema of the graph.*

== Exercise 7.8: View the current schema of the graph (Solution)

*View the current schema of the graph.*

[source, cypher]
----
call db.schema
----

The result returned should be:

[.thumb]
image::{guides}/img/SchemaAfterAddingRelatiobnships.png[SchemaAfterAddingRelatiobnships,width=400]



++++
<div style="clear: both;"></div>
++++

== Exercise 7.9: Retrieve the names and roles for actors (Instructions)

*Query the graph to return the names and roles of actors in the movie, _Forrest Gump_.*

== Exercise 7.9: Retrieve the names and roles for actors (Solution)

*Query the graph to return the names and roles of actors in the movie, _Forrest Gump_.*

[source, cypher]
----
MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump'
RETURN p.name, rel.roles
----

The result returned should be:

[.thumb]
image::{guides}/img/ForrestGumpRoles.png[ForrestGumpRoles,width=400]



++++
<div style="clear: both;"></div>
++++

== Exercise 7.10: Retrieve information about any specific relationships (Instructions)

*Query the graph to retrieve information about any _HELPED_ relationships.*

== Exercise 7.10: Retrieve information about any specific relationships (Solution)

*Query the graph to retrieve information about any _HELPED_ relationships.*

[source, cypher]
----
MATCH (p1:Person)-[rel:HELPED]-(p2:Person)
RETURN p1.name, rel, p2.name
----

The result returned should be:

[.thumb]
image::{guides}/img/HELPEDProperties.png[HELPEDProperties,width=400]



++++
<div style="clear: both;"></div>
++++

Notice here that 2 results are returned.
That is because we did not specify a direction for the relationship in the query.
So there is a relationship from _Tom Hanks_ and also to _Gary Sinise_, even though there is only one physical relationship in the graph.
If you specify a direction for the relationship, only one result is returned.

== Exercise 7.11: Modify a property of a relationship (Instructions)

Next, you will modify existing properties for a relationship and also remove them.

*Modify the _role_ that _Gary Sinise_ played in the movie, _Forrest Gump_ from _Lieutenant Dan Taylor_ to _Lt. Dan Taylor_.*

== Exercise 7.11: Modify a property of a relationship (Solution)

*Modify the _role_ that _Gary Sinise_ played in the movie, _Forrest Gump_ from _Lieutenant Dan Taylor_ to _Lt. Dan Taylor_.*

[source, cypher]
----
MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump' AND p.name = 'Gary Sinise'
SET rel.roles =['Lt. Dan Taylor']
----

The result returned should be:

[.thumb]
image::{guides}/img/UpdateGarySiniseRole.png[UpdateGarySiniseRole,width=400]



++++
<div style="clear: both;"></div>
++++

== Exercise 7.12: Remove a property from a relationship (Instructions)

*Remove the _research_ property from the _HELPED_ relationship from _Tom Hanks_ to _Gary Sinise_.*

== Exercise 7.12: Remove a property from a relationship (Solution)

*Remove the _research_ property from the _HELPED_ relationship from _Tom Hanks_ to _Gary Sinise_.*

[source, cypher]
----
MATCH (p1:Person)-[rel:HELPED]->(p2:Person)
WHERE p1.name = 'Tom Hanks' AND p2.name = 'Gary Sinise'
REMOVE rel.research
----

The result returned should be:

[.thumb]
image::{guides}/img/RemoveResearchProperty.png[RemoveResearchProperty,width=400]



++++
<div style="clear: both;"></div>
++++

== Exercise 7.13: Confirm that your modifications were made to the graph (Instructions)

*Query the graph to confirm that your modifications were made to the graph.*

== Exercise 7.13: Confirm that your modifications were made to the graph (Solution)

*Query the graph to confirm that your modifications were made to the graph.*

[source, cypher]
----
MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump'
return p, rel, m
----

The result returned should be:

[.thumb]
image::{guides}/img/GarySiniseRoleUpdate.png[GarySiniseRoleUpdate,width=400]



++++
<div style="clear: both;"></div>
++++

== Exercise 7: Creating Relationships (Taking it further - optional)

* Try adding or updating properties using the JSON-style syntax using `=` and `+=`.

== Exercise 7: Creating Relationships (Summary)

In this exercise you created relationships between nodes.
Then you added properties to relationships.
Finally, you removed properties from relationships.

pass:a[<a play-topic='{guides}/08.html'>Continue to Exercise 8</a>]
