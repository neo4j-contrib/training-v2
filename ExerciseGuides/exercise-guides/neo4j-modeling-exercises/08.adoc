= Exercise 8
:icons: font

== Exercise 8: Refactoring large graphs  (Preparations)

Before you begin this exercise, make sure that you have performed the refactoring from Exercise 7.

This is what you should see when you click the database icon image:{guides}/img/database-icon.png[].

image::{guides}/img/AfterExercise7.png[AfterExercise7,width=150,role=left]

*If your database does not have the same nodes and relationships, you can execute these Cypher statements to reset your graph to what it should be before you start this exercise.*

[source,cypher]
----
include::scripts/AfterExercise_7.cypher[]
----

== Exercise 8: Refactoring large graphs (Overview)

As your graph gets larger and you profile the queries that are important to your use cases, you will find that the refactoring is not possible without the ability to batch the refactoring into chunks of processing.
The APOC library has a number of procedures that are useful for refactoring a graph.
The procedure that you use to process batches of work on a graph is `apoc.periodic.commit()`.
In this exercise, you will load 100K flights into the graph and use APOC to help  you refactor the graph.


Here are the tasks you will perform:

* *Exercise 8.1*: Load more airports into the graph.
* *Exercise 8.2*: Load 100K flights into the graph.
* *Exercise 8.3*: Prepare the `Flight` nodes for batch processing.
* *Exercise 8.4*: Refactor the graph to use `AirportDay` by batching commits.
* *Exercise 8.5*: Prepare the `AirportDay` nodes for batch processing.
* *Exercise 8.6*: Refactor the graph to use specific relationships by batching commits.
* *Exercise 8.7*: Profile the query that finds flights on a specific day using the general relationship.
* *Exercise 8.8*: Profile the query that finds flights on a specific day using the specific relationship.

Go to the next page to start this exercise.

== Exercise 8.1:  Load more airports into the graph. (Instructions)

*Execute this Cypher statement to load the Airport nodes from the CSV file*:

[source,cypher]
----
LOAD CSV WITH HEADERS FROM 'https://r.neo4j.com/flights_2019_100k'  AS row
UNWIND [row.Origin, row.Dest] AS airport
WITH DISTINCT airport
MERGE (:Airport {code: airport})
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex8_LoadAirports.png[Ex8_LoadAirports,width=300]

== Exercise 8.2:  Load 100k flights into the graph. (Instructions)

*Execute this Cypher statement to load the Flight nodes from the CSV file*:

[source,cypher]
----
USING PERIODIC COMMIT 10000
LOAD CSV WITH HEADERS FROM 'https://r.neo4j.com/flights_2019_100k' AS row
MATCH (origin:Airport {code: row.Origin})
MATCH (destination:Airport {code: row.Dest})
MERGE (newFlight:Flight { flightId: row.UniqueCarrier + row.FlightNum + '_' + row.Year + '-' + row.Month + '-' + row.DayofMonth + '_' + row.Origin + '_' + row.Dest }   )
ON CREATE SET newFlight.date = toInteger(row.Year) + '-' + toInteger(row.Month) + '-' + toInteger(row.DayofMonth),
              newFlight.airline = row.UniqueCarrier,
              newFlight.number = row.FlightNum,
              newFlight.departure = toInteger(row.CRSDepTime),
              newFlight.arrival = toInteger(row.CRSArrTime)
MERGE (origin)<-[:ORIGINATES_FROM]-(newFlight)
MERGE (newFlight)-[:LANDS_IN]->(destination)
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex8_LoadFlights.png[Ex8_LoadFlights,width=300]

== Exercise 8.3:  Prepare the `Flight` nodes for batch processing. (Instructions)

*Execute this Cypher code to add the `Process` label to all `Flight` nodes that will be processed for the batch refactoring:*

[source,cypher]
----
MATCH (f:Flight)
SET f:Process
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex8_PrepareFlights.png[Ex8_PrepareFlights,width=300]

== Exercise 8.4:  Refactor the graph to use `AirportDay` by batching commits. (Instructions)

Here is the code that you used previously to refactor the graph to use Airport day:

[source,cypher]
----
MATCH (origin:Airport)<-[:ORIGINATES_FROM]-(flight:Flight)-
       [:LANDS_IN]->(destination:Airport)
MERGE (originAirportDay:AirportDay
      {airportDayId: origin.code + '_' + flight.date})
SET originAirportDay.date = flight.date
MERGE (destinationAirportDay:AirportDay
       {airportDayId: destination.code + '_' + flight.date})
SET destinationAirportDay.date = flight.date
MERGE (origin)-[:HAS_DAY]->(originAirportDay)
MERGE (flight)-[:ORIGINATES_FROM]->(originAirportDay)
MERGE (flight)-[:LANDS_IN]->(destinationAirportDay)
MERGE (destination)-[:HAS_DAY]->(destinationAirportDay)
----

*Modify this Cypher code to batch process the `Flight` nodes in batches of 500 and execute the code.*

*Hint*: `Use apoc.periodic.commit()`.

== Exercise 8.4:  Refactor the graph to use `AirportDay` by batching commits. (Solution)

*Modify this Cypher code to batch process the `Flight` nodes in batches of 500 and execute the query.*

*Hint*: `Use apoc.periodic.commit()`.

[source,cypher]
----
CALL apoc.periodic.commit('
MATCH (flight:Process)
WITH flight LIMIT {limit}

MATCH (origin:Airport)<-[:ORIGINATES_FROM]-(flight)-[:LANDS_IN]->(destination:Airport)

MERGE (originAirportDay:AirportDay {airportDayId: origin.code + "_" + flight.date})
ON CREATE SET originAirportDay.date = flight.date

MERGE (destinationAirportDay:AirportDay {airportDayId: destination.code + "_" + flight.date})
ON CREATE SET destinationAirportDay.date = flight.date

MERGE (origin)-[:HAS_DAY]->(originAirportDay)
MERGE (originAirportDay)<-[:ORIGINATES_FROM]-(flight)
MERGE (flight)-[:LANDS_IN]-(destinationAirportDay)
MERGE (destination)-[:HAS_DAY]->(destinationAirportDay)

REMOVE flight:Process
RETURN COUNT(*)

',{limit:500}
)
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex8_RefactorAirportDay.png[Ex8_RefactorAirportDay,width=300]

== Exercise 8.5:  Prepare the `AirportDay` nodes for batch processing. (Instructions)

*Execute this Cypher code to add the `Process` label to all `AirportDay` nodes that will be processed for the batch refactoring:*

[source,cypher]
----
MATCH (ad:AirportDay)
SET ad:Process
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex8_PrepareAirportDays.png[Ex8_PrepareAirportDays,width=300]

== Exercise 8.6:  Refactor the graph to use specific by batching commits. (Instructions)

*To make the refactoring simpler, first delete all existing specific relationships by executing this Cypher statement:*

[source,cypher]
----
MATCH (airport:Airport)-[r]->(:AirportDay)
WHERE NOT TYPE(r) = 'HAS_DAY'
DELETE r
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex8_DeleteSpecificRelationships.png[Ex8_DeleteSpecificRelationships,width=300]

Here is the code that you used previously to refactor the graph to use specific relationships:

[source,cypher]
----
MATCH (origin:Airport)-[hasDay:HAS_DAY]->(ad:AirportDay)
CALL apoc.create.relationship(startNode(hasDay),
                              'ON_' + ad.date,
                              {},
                              endNode(hasDay) ) YIELD rel
RETURN COUNT(*)
----

*Modify this Cypher code to batch process the `AirportDay` nodes in batches of 500 and execute the code.*

*Hint*: Use `apoc.periodic.commit()`.

== Exercise 8.6:  Refactor the graph to use specific by batching commits. (Solution)

*Modify this Cypher code to batch process the `AirportDay` nodes in batches of 500 and execute the code.*

*Hint*: Use `apoc.periodic.commit()`.

[source,cypher]
----
CALL apoc.periodic.commit('
  MATCH (ad:Process)
  WITH ad LIMIT {limit}

  MATCH (origin:Airport)-[hasDay:HAS_DAY]->(ad:AirportDay)
  CALL apoc.create.relationship(startNode(hasDay), "ON_" + ad.date, {}, endNode(hasDay) ) YIELD rel

  REMOVE ad:Process
  RETURN COUNT(*)
',{limit:500})
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex8_RefactorSpecificRelationships.png[Ex8_RefactorSpecificRelationships,width=300]

== Exercise 8.7:  Profile the query that finds flights on a specific day using the general relationship. (Instructions)

Previously, we used this query to look for all flights that go from Las Vegas (LAS) to Chicago (MDW) on January 3, 2019 (2019-1-3).

*Execute this Cypher query*:

[source,cypher]
----
PROFILE MATCH (origin:Airport {code: 'LAS'})-[:HAS_DAY]->(:AirportDay
      {date: '2019-1-3'})<-[:ORIGINATES_FROM]-(flight:Flight),
      (flight)-[:LANDS_IN]->(:AirportDay
      {date: '2019-1-3'})<-[:HAS_DAY]-(destination:Airport {code: 'MDW'})
RETURN origin, destination, flight
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex8_GeneralRelationshipsQuery.png[Ex8_GeneralRelationshipsQuery,width=150]

Previously on the graph with 10k flights, this query had 1813 db hits.
After loading 100k Flights, we see 1863 db hits.

== Exercise 8.8:  Profile the query that finds flights on a specific day using the specific relationship. (Instructions)

Previously, we used this query to look for all flights that go from Las Vegas (LAS) to Chicago (MDW) on January 3, 2019 (2019-1-3).

*Execute this Cypher query*:

[source,cypher]
----
PROFILE
MATCH (origin:Airport {code: 'LAS'})-
[:`ON_2019-1-3`]->(originDay:AirportDay),
(originDay)<-[:ORIGINATES_FROM]-(flight:Flight),
(flight)-[:LANDS_IN]->(destinationDay),
(destinationDay:AirportDay)<-[:`ON_2019-1-3`]-
(destination:Airport {code: 'MDW'})
RETURN flight.date, flight.number, flight.airline,
flight.departure, flight.arrival
ORDER BY flight.date, flight.departure
----

The result returned should be:

[.thumb]
image::{guides}/img/Ex8_SpecificRelationshipsQuery.png[Ex8_SpecificRelationshipsQuery,width=150]

Previously on the graph with 10k flights, this query had 1716 db hits.
After loading 100k Flights, we see 1716 db hits again, probably due to the fact that we did not add any more flights on this date.

== Exercise 8: Refactoring large graphs  (Summary)

In this exercise, you loaded the graph with 100k flights.
Then you used `apoc.periodic.commit()` to refactor the graph in batches of 500 where you added the `AirportDay` nodes and you added specific relationships.
You reran the query that looked for flights on a specific day and found that using the specific relationship was faster than using the general relationship.

Whenever you change the model and refactor the graph, you must profile your queries to ensure that the changes you have made to the graph will be beneficial.

pass:a[<a play-topic='{guides}/09.html'>Continue to Exercise 9</a>]

